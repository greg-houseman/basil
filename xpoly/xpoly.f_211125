C   "boundary"
C   routine xpoly used to make a basic poly file from a set of
C   coordinate pairs, assumed to form a closed polygon provided
C   in sequence, as provided in the file called "boundary".  
C   The final point will be joined to the first point. Avoid 
C   any duplicate points or points that are too close to
C   each other, e.g. do not repeat the first point at the end.
C
C   "internal"
C   Has now been amended to allow for multiple internal stuctures
C   that may be open or closed, but are assumed to be non-intersecting
C   and entirely within the external boundary. Sets of coordinate 
C   pairs for distinct structures are separated by a single line
C   with '#' in column 1.  'C' in column 2 of that line means the
C   structure is closed.  An  integer that follows on the same line
C   is used as a label for that structure.
C
C   "connections"
C   Now also amended to allow for internal boundary segments that are 
C   connected to points that are already defined in "boundary" or
C   "internal".  Each connecting segment starts and ends with a point
C   that is already present - as determined by having a location
C   sufficiently close to an existing point in (x,y) space.
C   Points are listed in consecutive pairs in file "connections"
C
C   To allow for multiple segments in the same file, please insert
C   between each segment a single line that has # in the first 
C   column. If 'D' is in the second column of that line the following
C   segment will be given an identifying label in the range
C   2000 - 2999, which is used by basil to signal discontinuous
C   properties.  To ensure the last segment is read properly please 
C   also terminate the file with # in the first column of the last
C   line.
C
C   multiple segments in connections file have been tested ok
C   multiple segments in internal file need further testing.
C   multiple segments in boundary file are not implemented yet
C
C   revised by G. Houseman, September 2021.
C   
      PARAMETER (NBND=500, MSEG=20)
      DIMENSION XBND(2*NBND),YBND(2*NBND),XCON(NBND),YCON(NBND)
      DIMENSION ICB(MSEG),ICB1(MSEG),ICE(MSEG),ICE1(MSEG)
      DIMENSION IBN(MSEG),IIN(MSEG),ICN(MSEG),ICLOSE(MSEG)
      DIMENSION INTB1(MSEG),INTBL(MSEG)
      CHARACTER(80) INSTRING,HEADER
      CHARACTER(6) STR
C
C    MSEG is the number of separately labelled segments
C    IBN(MSEG), IIN(MSEG) are the label numbers that
C    get attached to each group of segments, for segments that
C    belong to external boundary, internal boundary, and connections
C
C    read the set of points on the external boundary
C
      OPEN(8,FILE='boundary',STATUS='OLD',ERR=900)
      NPT=0
   10 CONTINUE
      NPT=NPT+1
      IF(NPT.GT.NBND)THEN
        WRITE(6,*)'Insufficient memory allocated, increase NBND'
        STOP
      ENDIF
      READ(8,*,END=20)XBND(NPT),YBND(NPT)
      GO TO 10
   20 CONTINUE
      NPT=NPT-1
      NBT=NPT
      WRITE(6,10021)NPT
10021 FORMAT('External boundary loop has ',13X,I4,' points')
      IF(NPT.LT.3)THEN
        WRITE(6,*)'Insufficient points are available'
        STOP
C
C    report external boundary points
C
        ELSE
C         DO K=1,NPT
C           WRITE(*,*)K,XBND(K),YBND(K)
C         ENDDO
      ENDIF
      CLOSE(8)
      NTN=NPT                                ! running total: no. of points
C
C    if there is a file called internal, then read this also.
C    distinct internal structures are separated by single character #
C    in column 1, preceding pairs of coordinates on subsequent lines.
C    end of data for a structure is indicated by the next # in column 1.
C    Note that first and last line of file should be a # in column 1.
C
C    A structure may be open or closed.  If closed, first and last points
C    are connected to each other, otherwise they are treated as end points
C    of the structure. A closed structure is indicated by C following #.
C    In absence of C, the structure is treated as open.
C
C    NOINT = number of distinct structures; INTB1(NOINT) and INTBL(NOINT)
C    are addresses of first and last points of structure NOINT.
C    Probably best to avoid interesting situations like intersecting
C    structures.
C
      NOINT=0
      NINTRNL=0
      OPEN(8,FILE='internal',STATUS='OLD',ERR=50)
      INTB1(1)=NPT+1
   30 CONTINUE
      READ(8,'(A80)',END=40)INSTRING(1:80)
C
C    close last loop, start next loop
C
      IF(INSTRING(1:1).EQ.'#')THEN
        HEADER=INSTRING
        IF(NOINT.GT.MSEG)THEN
          WRITE(6,*)'Insufficient memory in xpoly, increase MSEG'
          STOP
        ENDIF
        NOINT=NOINT+1                             ! structure index
        ICLOSE(NOINT)=0
        IF(INSTRING(2:2).EQ.'C')ICLOSE(NOINT)=1   ! switch for closed loop
        INTB1(NOINT)=NPT+1                        ! identify first point
      ELSE
        READ(INSTRING,*,ERR=40,END=40)XI2,YI2
        IF(NPT.GE.NBND)THEN
          WRITE(6,*)'Insufficient memory in xpoly, increase NBND'
          STOP
        ENDIF
        NPT=NPT+1
        IF(NPT.EQ.(INTB1(NOINT)+1))READ(HEADER(3:20),*,END=90)IIN(NOINT)
        XBND(NPT)=XI2
        YBND(NPT)=YI2
        INTBL(NOINT)=NPT                        ! last point unless replaced
      ENDIF
C
C   go and read next line
C
      GO TO 30
   40 CONTINUE
      CLOSE(8)
C
C   decrement NOINT if there was a final # without more points
C
      IF(INTB1(NOINT).GT.NPT)NOINT=NOINT-1
C
C    check points in each structure
C
      IF(NOINT.GT.0)THEN
        DO I=1,NOINT
          NIB=INTBL(I)-INTB1(I)+1                    ! points added by this segment
          NTN=NTN+NIB                                ! accummulate total points
          NINTRNL=NINTRNL+NIB+(ICLOSE(I)-1)          ! accummulate total segments
          WRITE(6,10019)I,IIN(I),NIB
10019     FORMAT('Internal structure ',I5,' label ',I5,' has ',I3,
     :           ' points')
          IF(NIB.LT.(2+ICLOSE(I)))THEN 
            WRITE(10029)I
10029       FORMAT('Too few points in structure ',I3,
     :           ' Check file: internal')
            STOP
          ENDIF
C
C    report additional points
C
C         DO K=INTB1(I),INTBL(I)
C           WRITE(*,*)K,XBND(K),YBND(K)
C         ENDDO
C
C    write output for a REG statement as used by basil
C
C         WRITE(6,*)'For REGION statement:'
          KO=INTB1(I)
          DO WHILE (KO.LE.INTBL(I))
            KE=KO+3
            IF(KE.GT.INTBL(I))KE=INTBL(I)
            WRITE(6,1050)(XBND(J),YBND(J),J=KO,KE)
 1050       FORMAT(8F9.3,' &')
            KO=KO+4
          ENDDO
        ENDDO
      ENDIF
C
C     jump to here if no internal file
C
   50 CONTINUE
C
C     if there is a file called connections, then read this also
C     this list of points assumed to begin and end on points already
C     defined in previous files.  Multiple segments may be included
C     sequentially provided each segment begins and ends on a pre-
C     defined point.  NTN is running total of distinct nodes
C
      NCSEG=0
      NSEG=0
      NCON=0
      OPEN(8,FILE='connections',STATUS='OLD',ERR=80)
   60 CONTINUE
      READ(8,'(A80)',END=80)INSTRING(1:80)
      IF((INSTRING(1:1).EQ.'#').OR.(NCON.EQ.0))THEN
      HEADER=INSTRING
C
C    if this condition entered, then operation uses previous set of points read
C
        IF((NOINT+1).GT.MSEG)THEN
          WRITE(6,*)'Insufficient memory in xpoly, increase MSEG'
          STOP
        ENDIF
C
C    sort out details of segment just finished
C
        IF(NCON.NE.0)THEN
          ICB1(NSEG)=NTN+1         ! node index of 1st internal point
          ICE1(NSEG)=NTN+KE-2      ! node index of last internal point
C
C    segments in the connections file should begin and end with points
C    already defined.  confirm first point already present
C
          CALL PREZENT(XCON(1),YCON(1),XBND,YBND,NPT,IPR)
          IF(IPR.EQ.0)THEN
            WRITE(*,10020)XCON(1),YCON(1)
10020       FORMAT('First point of connections (',G12.5,
     :             ',',G12.5,') not previously defined')
            WRITE(*,*)'xpoly ignores the rest of the connections file'
            STOP
          ELSE
            ICB(NSEG)=IPR     ! node index of first point
          END IF
C
C   look for next point in connections file that is already present
C
          DO K=2,KE
            CALL PREZENT(XCON(K),YCON(K),XBND,YBND,NPT,IPR)
            IF(IPR.NE.0)GO TO 75
          ENDDO
          WRITE(*,*)'End point in connections file not found'
          STOP
C
C   points of current segment are in XCON(1:KE), YCON(1:KE)
C   add new points to XBND and YBND
C
   75     CONTINUE
          ICE(NSEG)=IPR      ! node index of last point
          WRITE(6,2020)ICN(NSEG),KE-2
 2020     FORMAT('Connection ',I4,' adds ',19X,I4,' points')
          IF(KE.GT.2)THEN
             DO K=1,KE-2
                XBND(NTN+K)=XCON(K+1)
                YBND(NTN+K)=YCON(K+1)
C               WRITE(*,*)NTN+K, XBND(NTN+K), YBND(NTN+K)
             ENDDO
             NPT=NPT+KE-2       ! update NPT used by PREZENT
C
C    write output for a REG statement as used by basil
C
C            WRITE(6,*)'For REGION statement - connections only!'
             WRITE(6,1050)XBND(ICB(NSEG)),YBND(ICB(NSEG)),
     :                   (XBND(J),YBND(J),J=NTN+1,NTN+KE-2),
     :                    XBND(ICE(NSEG)),YBND(ICE(NSEG))
             WRITE(6,2021)ICB(NSEG),XBND(ICB(NSEG)),YBND(ICB(NSEG)),
     :                    ICE(NSEG),XBND(ICE(NSEG)),YBND(ICE(NSEG))
 2021        FORMAT(' Connected to points: ',I4,' (',F9.3,',',F9.3,') ',
     :              ' and ',I4,' (',F9.3,',',F9.3,')')
          ELSE IF(KE.LT.2)THEN
            ICB1(NSEG)=0            ! key for no internal nodes on segment
            WRITE(6,1011)NSEG
 1011       FORMAT('Insufficient points in segment ',I2,' of ',
     :             'connections')
            STOP
          ENDIF
          NTN=NTN+KE-2
          NCSEG=NCSEG+KE-1
        ENDIF        ! if NCON.ne.0
C
C    initiate a new segment
C
        KE=0
        NSEG=NSEG+1
        NOINT=NOINT+1                      ! structure index
        NCON=NCON+1
        ICB(NSEG)=0
C
C    points for a new segment are first written to XCON,YCON(NCT)
C
      ELSE
        KE=KE+1
        IF(KE.GT.1)READ(HEADER(3:20),*,END=90)ICN(NSEG)
        IF(KE.GT.NBND)THEN
          WRITE(6,*)'Insufficient memory in xpoly, increase NBND'
          STOP
        ENDIF
        READ(INSTRING,*,END=80)XCON(KE),YCON(KE)
      ENDIF
C
C     look for more lines to read
C
      GO TO 60
      CLOSE(8)
C
C     skip to here if no connections file; begin writing poly file
C
   80 CONTINUE
      IF(ICB(NSEG).EQ.0)NSEG=NSEG-1
      STR=' 2 0 0'
      OPEN(10,FILE='x.poly')
      WRITE(10,1010)NTN,STR
 1010 FORMAT(I5,A6)
C
C   write set of all saved points
C
      DO K=1,NTN
        WRITE(10,1020)K,XBND(K),YBND(K)
      ENDDO
 1020 FORMAT(I5,X,2F12.5)
C
C   write total segment number
C
      WRITE(10,1030)NBT+NINTRNL+NCSEG
 1030 FORMAT(I5,' 1')
C
C   write external boundary segments
C
      IB=1
      DO K=1,NBT-1
        WRITE(10,1040)K,K,K+1,IB
      ENDDO
      WRITE(10,1040)NBT,NBT,1,IB
 1040 FORMAT(3I4,I3)
      NS=NBT                                ! segment number for output file
C
C   write internal structure segments
C 
      IF(NOINT.GT.0)THEN
        DO J=1,NOINT
          DO K=INTB1(J),INTBL(J)-1
            NS=NS+1
            WRITE(10,1041)NS,K,K+1,IIN(J)
          ENDDO
          IF(ICLOSE(J).EQ.1)THEN
            NLST=INTBL(J)
            NS=NS+1
            WRITE(10,1041)NS,NLST,INTB1(J),IIN(J)
 1041       FORMAT(3I4,I5)
          ENDIF
        ENDDO
      ENDIF
C
C   write additional connecting segments as needed
C
      IF(NSEG.GT.0)THEN
        DO J=1,NSEG
          NS=NS+1
          IF(ICB1(J).NE.0)THEN
            WRITE(10,1041)NS,ICB(J),ICB1(J),ICN(J)    ! 1st segment
            KA=ICB1(J)
            DO WHILE (KA.LT.ICE1(J))
              KB=KA+1
              NS=NS+1
              WRITE(10,1041)NS,KA,KB,ICN(J)           ! internal segments
              KA=KA+1
            ENDDO
            NS=NS+1
            WRITE(10,1041)NS,ICE1(J),ICE(J),ICN(J)    ! last segment
          ELSE
            WRITE(10,1041)NS,ICB(J),ICE(J),ICN(J)     ! one segment only
          ENDIF
        ENDDO
      END IF
C
C    finish and close file
C
      WRITE(10,*)'0'
      WRITE(10,*)'0'
      CLOSE(10)
      WRITE(*,*)'A new poly file has been written to x.poly'
C
      STOP
   90 WRITE(*,*)'please refer specification of segment labels ',
     :   ' needed in files internal, connections, following #_'
      STOP
  900 WRITE(*,*)'xpoly requires at least a file called boundary'
      STOP
      END
      SUBROUTINE PREZENT(XX,YY,XBND,YBND,NPT,IPR)
C
C   this subroutine checks if the point (XX,YY) is already present
C   in the list of points in (XBND(NPT), YBND(NPT))
C
      DIMENSION XBND(NPT),YBND(NPT)
      IPR=0
      DO J=1,NPT
        IF(SQRT((XX-XBND(J))*(XX-XBND(J)) + 
     :            (YY-YBND(J))*(YY-YBND(J))).LE.0.0001)THEN
          IPR=J
        ENDIF
      ENDDO
      RETURN
      END
